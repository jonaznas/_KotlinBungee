plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id "net.minecrell.plugin-yml.bungee" version "0.3.0"
}

sourceCompatibility = 1.8
shadowJar.archiveName = 'plugin.jar'

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile group: 'net.md-5', name: 'bungeecord-api', version: '1.8-SNAPSHOT'
    compile group: "com.zaxxer", name: "HikariCP", version: "3.4.1"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61"
    compile "com.github.hazae41:mc-kutils:master-SNAPSHOT"
    // compile "ch.qos.logback:logback-classic:1.2.1" //uncomment to get deep debug and trace logs
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

jar {
    actions = []
    dependsOn = []
    dependsOn('shadowJar')
}

clean {
    group = "_debug"
    delete "debug/server/"
}

task setupDevServer(type: Copy) {
    from "debug/config"
    into "debug/server"
}

task copyPluginToTestserver(dependsOn: ['build'], type: Copy) {
    from "${buildDir}/libs"
    into "debug/server/plugins"
}

task startDevServer(dependsOn: ['copyPluginToTestserver', 'setupDevServer'], type: JavaExec) {
    group = "_debug"
    main = "-jar"
    workingDir = "debug/server/"
    args = ["bungeecord.jar"]
    standardInput = System.in
}

bungee {
    main = 'plugin.Main'
    author = 'Jonaznas'
    // more configurations https://github.com/Minecrell/plugin-yml
}
